#version 450

layout (local_size_x = 16, local_size_y = 16) in;

layout (push_constant, std430, row_major) uniform pushConstants {
	int stage;
	int pingpong;
	int direction;
} constants;

struct complex {
	float real;
	float im;
};

complex mul(complex c0, complex c1) {
	complex c;
	c.real = c0.real * c1.real - c0.im * c1.im;
	c.im = c0.real * c1.im + c0.im * c1.real;
	return c;
}

complex add(complex c0, complex c1) {
	complex c;
	c.real = c0.real + c1.real;
	c.im = c0.im + c1.im;
	return c;
}

void horizontalButterflies() {
	complex H;
	ivec2 x = ivec2(gl_GlobalInvocationID.xy);

	if (constants.pingpong == 0) {
		vec4 data = imageLoad(twiddlesIndices, ivec2(constants.stage, x.x)).rgba;
		vec2 p_ = imageLoad(pingpong0, ivec2(data.z, x.y)).rg;
		vec2 q_ = imageLoad(pingpong0, ivec2(data.w, x.y)).rg;
		vec2 w_ = vec2(data.x, data.y);

		complex p = complex(p_.x, p_.y);
		complex q = complex(q_.x, q_.y);
		complex w = complex(w_.x, w_.y);

		// Butterfly operation
		H = add(p,mul(w,q));

		imageStore(pingpong1, x, vec4(H.real, H.im, 0, 1));
	} else if (constants.pingpong == 1) {
		vec4 data = imageLoad(twiddlesIndices, ivec2(constants.stage, x.x)).rgba;
		vec2 p_ = imageLoad(pingpong1, ivec2(data.z, x.y)).rg;
		vec2 q_ = imageLoad(pingpong1, ivec2(data.w, x.y)).rg;
		vec2 w_ = vec2(data.x, data.y);

		complex p = complex(p_.x, p_.y);
		complex q = complex(q_.x, q_.y);
		complex w = complex(w_.x, w_.y);

		// Butterfly operation
		H = add(p,mul(w,q));

		imageStore(pingpong0, x, vec4(H.real, H.im, 0, 1));
	}
}

void verticalButterflies() {
	complex H;
	ivec2 x = ivec2(gl_GlobalInvocationID.xy);

	if (constants.pingpong == 0) {
		vec4 data = imageLoad(twiddlesIndices, ivec2(constants.stage, x.y)).rgba;
		vec2 p_ = imageLoad(pingpong0, ivec2(x.x, data.z)).rg;
		vec2 q_ = imageLoad(pingpong0, ivec2(x.x, data.w)).rg;
		vec2 w_ = vec2(data.x, data.y);

		complex p = complex(p_.x, p_.y);
		complex q = complex(q_.x, q_.y);
		complex w = complex(w_.x, w_.y);

		// Butterfly operation
		H = add(p,mul(w,q));

		imageStore(pingpong1, x, vec4(H.real, H.im, 0, 1));
	} else if (constants.pingpong == 1) {
		vec4 data = imageLoad(twiddlesIndices, ivec2(constants.stage, x.y)).rgba;
		vec2 p_ = imageLoad(pingpong1, ivec2(x.x, data.z)).rg;
		vec2 q_ = imageLoad(pingpong1, ivec2(x.x, data.w)).rg;
		vec2 w_ = vec2(data.x, data.y);

		complex p = complex(p_.x, p_.y);
		complex q = complex(q_.x, q_.y);
		complex w = complex(w_.x, w_.y);

		// Butterfly operation
		H = add(p,mul(w,q));

		imageStore(pingpong0, x, vec4(H.real, H.im, 0, 1));
	}
}

void main() {
	if (constants.direction == 0) {
		horizontalButterflies();
	} else if (constants.direction == 1) {
		verticalButterflies();
	}
}
